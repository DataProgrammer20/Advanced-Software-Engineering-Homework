-- This is a USE model that has embedded SOIL operations in it
-- Question 1

model MovieRental

enum PriceCode {regular, family, newRelease}

-- classes
-- Customer class
class Customer
attributes
  name:String
  numRen:Integer
operations
  getTotalCharge():Real
    begin
      declare sum: Real, charge: Real;
      sum:=0.0;
      for ren in self.rentals do
        charge:=ren.getCharge();
        sum:=sum+charge;
      end;
      result := sum;
    end
  addRental()
   begin
   end
  getName()
    begin
    end
  Statement()
    begin
      declare 
      aCharge:Charge, sm:Movie, ch:Real, t:String;
      self.numRen:=self.rentals->size();
      for ren in self.rentals do
        ch:=ren.getCharge();
        sm:=ren.getMovie();
        t:=sm.getTitle();
        aCharge:= new Charge;
        aCharge.chVal:=ch;
        aCharge.chT:=t;
        insert(self,aCharge) into customerCharges
      end
    end
end

-- Rental class
class Rental 
attributes
  daysRented:Integer
operations
  getDaysRented(): Integer
    begin
      result := self.daysRented;
    end
  getMovie(): Movie
    begin
      result := self.movie;
    end
  getCharge(): Real
    begin
     declare wrkCh:Real, m:Movie, pc:PriceCode,dy:Integer;
     m:=self.getMovie();
     dy:=self.getDaysRented();
     pc:=m.getPriceCode();
     wrkCh:=0;
     if pc=PriceCode::regular then
       wrkCh:=2.0;
       if dy > 2 then
         wrkCh:=wrkCh + (dy -2) * 1.5;
       end;
     end;
     if pc=PriceCode::family then
       wrkCh:=1.5;
       if dy > 3 then
         wrkCh:=wrkCh + (dy -3) * 1.5;
       end;
     end;
     if pc=PriceCode::newRelease then
       wrkCh:=dy * 3.0;
     end;
     result:=wrkCh;
    end
end

-- Movie class
class Movie
attributes
  title:String
  priceCode:PriceCode
operations
  getPriceCode():PriceCode
    begin
      result := self.priceCode;
    end
  setPriceCode(code:PriceCode)
    begin
      self.priceCode := code;
    end
getTitle():String
  begin
    result := self.title;
  end
end

-- Charge class
class Charge
attributes
  chVal:Real
  chT: String
operations
end

--associations

association custRentals between
  Customer [1] role renter
  Rental [0..*] role rentals
end

association movRental between
  Rental [0..*] role movRentals
  Movie [1] role movie
end

association customerCharges between
  Customer [1] role cust
  Charge [0..*] role charges
end